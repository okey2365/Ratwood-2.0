SUBSYSTEM_DEF(mapping)
	name = "Mapping"
	init_order = INIT_ORDER_MAPPING
	flags = SS_NO_FIRE

	var/list/nuke_tiles = list()
	var/list/nuke_threats = list()

	/// The current map config the server loaded at round start.
	var/datum/map_config/current_map

	var/datum/map_adjustment/map_adjustment	//all the tweaks and changes for roles across various maps

	var/list/map_templates = list()
	var/list/map_load_marks = list() //The game scans thru the map and looks for marks, then adds them to this list for caching

	/// trait signature -> list of /datum/shared_z_stack, see LoadOtherZ
	var/list/shared_z_stacks = list()

	var/list/ruins_templates = list()
	var/datum/space_level/isolated_ruins_z //Created on demand during ruin loading.

	var/list/shuttle_templates = list()
	var/list/shelter_templates = list()

	var/list/areas_in_z = list()

	var/list/turf/unused_turfs = list()				//Not actually unused turfs they're unused but reserved for use for whatever requests them. "[zlevel_of_turf]" = list(turfs)
	var/list/datum/turf_reservations		//list of turf reservations
	var/list/used_turfs = list()				//list of turf = datum/turf_reservation

	var/list/reservation_ready = list()
	var/clearing_reserved_turfs = FALSE

	// Z-manager stuff
	var/station_start  // should only be used for maploading-related tasks
	var/space_levels_so_far = 0
	///list of all z level datums in the order of their z (z level 1 is at index 1, etc.)
	var/list/datum/space_level/z_list
	///list of all z level indices that form multiz connections and whether theyre linked up or down
	///list of lists, inner lists are of the form: list("up or down link direction" = TRUE)
	var/list/multiz_levels = list()
	var/datum/space_level/transit
	var/datum/space_level/empty_space
	var/num_of_res_levels = 1
	/// True when in the process of adding a new Z-level, global locking
	var/adding_new_zlevel = FALSE

	///this is a list of all the world_traits we have from things like god interventions
	var/list/active_world_traits = list()
	///antag retainer
	var/datum/antag_retainer/retainer

//dlete dis once #39770 is resolved
/datum/controller/subsystem/mapping/proc/HACK_LoadMapConfig()
	if(!current_map)
#ifdef FORCE_MAP
		current_map = load_map_config(FORCE_MAP)
#else
		current_map = load_map_config(error_if_missing = FALSE)
#endif

/datum/controller/subsystem/mapping/PreInit()
	HACK_LoadMapConfig()
	// After assigning a config datum to var/config, we check which map ajudstment fits the current config
	if(islist(current_map.map_file) && length(current_map.map_file))
		current_map.map_file = current_map.map_file[1]
	for(var/datum/map_adjustment/each_adjust as anything in subtypesof(/datum/map_adjustment))
		if(current_map.map_file && initial(each_adjust.map_file_name) != current_map.map_file)
			continue
		map_adjustment = new each_adjust() // map_adjustment has multiple procs that'll be called from needed places (i.e. job_change)
		log_world("Loaded '[current_map.map_file]' map adjustment.")
		break
	return ..()

/datum/controller/subsystem/mapping/Initialize(timeofday)
	retainer = new
	if(initialized)
		return
	if(current_map.defaulted)
		var/datum/map_config/old_config = current_map
		current_map = global.config.defaultmap
		if(!current_map || current_map.defaulted)
			to_chat(world, "<span class='boldannounce'>Unable to load next or default map config, defaulting to [old_config.map_name] </span>")
			current_map = old_config
	if(map_adjustment)
		map_adjustment.on_mapping_init()
		SSregionthreat?.on_map_ready()
		log_world("Applied '[map_adjustment.map_file_name]' map adjustment: on_mapping_init()")
	loadWorld()
	repopulate_sorted_areas()
	process_teleport_locs()			//Sets up the wizard teleport locations
	preloadTemplates()
	// Add the transit level
	transit = add_new_zlevel("Transit/Reserved", list(ZTRAIT_RESERVED = TRUE))
	repopulate_sorted_areas()
	initialize_reserved_level(transit.z_value)
	generate_z_level_linkages()
	return ..()

/datum/controller/subsystem/mapping/proc/generate_z_level_linkages()
	for(var/z_level in 1 to length(z_list))
		generate_linkages_for_z_level(z_level)

/datum/controller/subsystem/mapping/proc/generate_linkages_for_z_level(z_level)
	if(!isnum(z_level) || z_level <= 0)
		return FALSE

	if(multiz_levels.len < z_level)
		multiz_levels.len = z_level

	var/z_above = level_trait(z_level, ZTRAIT_UP)
	var/z_below = level_trait(z_level, ZTRAIT_DOWN)
	if(!(z_above == TRUE || z_above == FALSE || z_above == null) || !(z_below == TRUE || z_below == FALSE || z_below == null))
		stack_trace("Warning, numeric mapping offsets are deprecated. Instead, mark z level connections by setting UP/DOWN to true if the connection is allowed")
	multiz_levels[z_level] = new /list(LARGEST_Z_LEVEL_INDEX)
	multiz_levels[z_level][Z_LEVEL_UP] = !!z_above
	multiz_levels[z_level][Z_LEVEL_DOWN] = !!z_below

/datum/controller/subsystem/mapping/Recover()
	flags |= SS_NO_INIT
	initialized = SSmapping.initialized
	map_templates = SSmapping.map_templates
	ruins_templates = SSmapping.ruins_templates
	shuttle_templates = SSmapping.shuttle_templates
	shelter_templates = SSmapping.shelter_templates
	unused_turfs = SSmapping.unused_turfs
	turf_reservations = SSmapping.turf_reservations
	used_turfs = SSmapping.used_turfs

	current_map = SSmapping.current_map

	clearing_reserved_turfs = SSmapping.clearing_reserved_turfs

	z_list = SSmapping.z_list

#define INIT_ANNOUNCE(X) to_chat(world, "<span class='boldannounce'>[X]</span>"); log_world(X)
/datum/controller/subsystem/mapping/proc/LoadGroup(list/errorList, name, path, files, map_folder, list/traits, list/default_traits, silent = FALSE)
	. = list()
	var/start_time = REALTIMEOFDAY

	if (!islist(files))  // handle single-level maps
		files = list(files)

	var/track_memory = !CONFIG_GET(flag/disable_memory_stats)

	// check that the total z count of all maps matches the list of traits
	var/total_z = 0
	var/list/parsed_maps = list()
	for (var/file in files)
		var/full_path = "[map_folder]/[path]/[file]"
		if(path == "custom")
			full_path = "data/custom_maps/[file]"
		var/parse_start = REALTIMEOFDAY
		var/parse_rss = track_memory ? get_process_rss_bytes() : null
		var/datum/parsed_map/pm = new(file(full_path))
		if(track_memory)
			log_map_memory("parse", full_path, parse_rss, parse_start)
		var/bounds = pm?.bounds
		if (!bounds)
			errorList |= full_path
			continue
		parsed_maps[pm] = total_z  // save the start Z of this file
		total_z += bounds[MAP_MAXZ] - bounds[MAP_MINZ] + 1

	if (!length(traits))  // null or empty - default
		for (var/i in 1 to total_z)
			traits += list(default_traits)
	else if (total_z != traits.len)  // mismatch
		INIT_ANNOUNCE("WARNING: [traits.len] trait sets specified for [total_z] z-levels in [path]!")
		if (total_z < traits.len)  // ignore extra traits
			traits.Cut(total_z + 1)
		while (total_z > traits.len)  // fall back to defaults on extra levels
			traits += list(default_traits)

	// preload the relevant space_level datums
	var/start_z = world.maxz + 1
	var/i = 0
	for (var/level in traits)
		add_new_zlevel("[name][i ? " [i + 1]" : ""]", level)
		++i

	// load the maps
	for (var/P in parsed_maps)
		var/datum/parsed_map/pm = P
		var/load_start = REALTIMEOFDAY
		var/load_rss = track_memory ? get_process_rss_bytes() : null
		if (!pm.load(1, 1, start_z + parsed_maps[P], no_changeturf = TRUE))
			errorList |= pm.original_path
		if(track_memory)
			log_map_memory("load", pm.original_path, load_rss, load_start)

	log_game("Loaded [name] in [(REALTIMEOFDAY - start_time)/10]s!")

	return parsed_maps

/// Distance kept between templates sharing a z-level, must exceed client view range
#define ZSTACK_PACK_MARGIN 20

/datum/shared_z_stack
	var/start_z
	var/z_count
	var/cursor_x = 1
	var/cursor_y = 1
	var/shelf_height = 0

/// Reserves a width x height footprint on this stack, returns list(x, y) or null if it cannot fit
/datum/shared_z_stack/proc/try_place(width, height)
	if (cursor_x + width - 1 > world.maxx)
		cursor_x = 1
		cursor_y += shelf_height + ZSTACK_PACK_MARGIN
		shelf_height = 0
	if (cursor_y + height - 1 > world.maxy || cursor_x + width - 1 > world.maxx)
		return null
	. = list(cursor_x, cursor_y)
	cursor_x += width + ZSTACK_PACK_MARGIN
	shelf_height = max(shelf_height, height)

/// Builds the compatibility key deciding which maps may share a z-stack:
/// z-count plus every per-level trait except the cosmetic Name
/datum/controller/subsystem/mapping/proc/z_stack_signature(list/traits)
	var/list/parts = list()
	for (var/list/level in traits)
		var/list/keys = list()
		for (var/key in level)
			if (key == "Name")
				continue
			keys += "[key]=[level[key]]"
		sortTim(keys, GLOBAL_PROC_REF(cmp_text_asc))
		parts += keys.Join(",")
	return "[length(traits)]z|[parts.Join("|")]"

/// Loads other_z map configs, automatically packing compatible templates onto shared
/// z-stacks by their parsed size instead of giving each its own full z-levels
/datum/controller/subsystem/mapping/proc/LoadOtherZ(list/errorList, list/configs)
	for (var/datum/map_config/conf in configs)
		if (islist(conf.map_file))  // multi-file configs use the classic loader untouched
			LoadGroup(errorList, conf.map_name, conf.map_path, conf.map_file, conf.map_folder, conf.traits, ZTRAITS_STATION)
			continue

		var/track_memory = !CONFIG_GET(flag/disable_memory_stats)
		var/start_time = REALTIMEOFDAY
		var/full_path = "[conf.map_folder]/[conf.map_path]/[conf.map_file]"
		var/parse_rss = track_memory ? get_process_rss_bytes() : null
		var/datum/parsed_map/pm = new(file(full_path))
		if (track_memory)
			log_map_memory("parse", full_path, parse_rss, start_time)
		var/list/bounds = pm?.bounds
		if (!bounds)
			errorList |= full_path
			continue
		var/width = bounds[MAP_MAXX] - bounds[MAP_MINX] + 1
		var/height = bounds[MAP_MAXY] - bounds[MAP_MINY] + 1
		var/z_count = bounds[MAP_MAXZ] - bounds[MAP_MINZ] + 1

		var/list/traits = conf.traits
		if (!islist(traits) || !length(traits))
			traits = list()
			for (var/i in 1 to z_count)
				traits += list(ZTRAITS_STATION)
		else if (z_count != traits.len)
			INIT_ANNOUNCE("WARNING: [traits.len] trait sets specified for [z_count] z-levels in [conf.map_path]!")
			if (z_count < traits.len)
				traits.Cut(z_count + 1)
			while (z_count > traits.len)
				traits += list(ZTRAITS_STATION)

		var/list/pos
		var/datum/shared_z_stack/stack
		var/signature = conf.no_z_sharing ? null : z_stack_signature(traits)
		if (signature)
			var/list/candidates = shared_z_stacks[signature]
			for (var/datum/shared_z_stack/candidate as anything in candidates)
				pos = candidate.try_place(width, height)
				if (pos)
					stack = candidate
					break

		if (!pos)
			stack = new
			stack.start_z = world.maxz + 1
			stack.z_count = z_count
			var/i = 0
			for (var/level in traits)
				add_new_zlevel("[conf.map_name][i ? " [i + 1]" : ""]", level)
				++i
			if (signature)
				var/list/candidates = shared_z_stacks[signature]
				if (!candidates)
					shared_z_stacks[signature] = candidates = list()
				candidates += stack
			pos = stack.try_place(width, height) || list(1, 1)

		var/load_start = REALTIMEOFDAY
		var/load_rss = track_memory ? get_process_rss_bytes() : null
		if (!pm.load(pos[1], pos[2], stack.start_z, no_changeturf = TRUE))
			errorList |= pm.original_path
		if (track_memory)
			log_map_memory("load", pm.original_path, load_rss, load_start)

		log_game("Loaded [conf.map_name] at [pos[1]],[pos[2]] z[stack.start_z] in [(REALTIMEOFDAY - start_time)/10]s!")

#undef ZSTACK_PACK_MARGIN

/datum/controller/subsystem/mapping/proc/loadWorld()
	//if any of these fail, something has gone horribly, HORRIBLY, wrong
	var/list/FailedZs = list()

	// ensure we have space_level datums for compiled-in maps
	InitializeDefaultZLevels()

	// load the station
	station_start = world.maxz + 1
	#ifdef TESTING
	INIT_ANNOUNCE("Loading [current_map.map_name]...")
	#endif

	LoadGroup(FailedZs, "Station", current_map.map_path, current_map.map_file, current_map.map_folder, current_map.traits, ZTRAITS_STATION)

	var/list/otherZ = list()

	#ifndef NO_DUNGEON
	otherZ += load_map_config("_maps/map_files/otherz/dungeon.json")
	#endif

	for(var/map_json in current_map.other_z)
		otherZ += load_map_config(map_json)
		log_world("Loaded '[current_map.other_z]' ")

	if(otherZ.len)
		for(var/datum/map_config/OtherZ in otherZ)
		LoadOtherZ(FailedZs, otherZ)

	if(SSdbcore.Connect())
		var/datum/DBQuery/query_round_map_name = SSdbcore.NewQuery({"
			UPDATE [format_table_name("round")] SET map_name = :map_name WHERE id = :round_id
		"}, list("map_name" = current_map.map_name, "round_id" = GLOB.round_id))
		query_round_map_name.Execute()
		qdel(query_round_map_name)

	#ifndef LOWMEMORYMODE
	// TODO: remove this when the DB is prepared for the z-levels getting reordered
	while (world.maxz < (5 - 1) && space_levels_so_far < current_map.space_ruin_levels)
		++space_levels_so_far
		add_new_zlevel("Empty Area [space_levels_so_far]", ZTRAITS_SPACE)

	#endif

	if(LAZYLEN(FailedZs))	//but seriously, unless the server's filesystem is messed up this will never happen
		var/msg = "RED ALERT! The following map files failed to load: [FailedZs[1]]"
		if(FailedZs.len > 1)
			for(var/I in 2 to FailedZs.len)
				msg += ", [FailedZs[I]]"
		msg += ". Yell at your server host!"
		INIT_ANNOUNCE(msg)
#undef INIT_ANNOUNCE

	// Custom maps are removed after station loading so the map files does not persist for no reason.
	if(current_map.map_path == "custom")
		fdel("data/custom_maps/[current_map.map_file]")



/*
/datum/controller/subsystem/mapping/proc/preloadTemplates(path = "_maps/templates/") //see master controller setup

	var/list/filelist = flist(path)
	for(var/map in filelist)
		var/datum/map_template/T = new(path = "[path][map]", rename = "[map]")
		map_templates[T.name] = T
*/

//Precache the templates via map template datums, not directly from files
//This lets us preload as many files as we want without explicitely loading ALL of them into cache (ie WIP maps or what have you)
/datum/controller/subsystem/mapping/proc/preloadTemplates()
	for(var/item in subtypesof(/datum/map_template)) //Look for our template subtypes and fire them up to be used later
		var/datum/map_template/template = new item()
		map_templates[template.id] = template


/datum/controller/subsystem/mapping/proc/RequestBlockReservation(width, height, z, type = /datum/turf_reservation, turf_type_override)
	UNTIL((!z || reservation_ready["[z]"]) && !clearing_reserved_turfs)
	var/datum/turf_reservation/reserve = new type
	if(turf_type_override)
		reserve.turf_type = turf_type_override
	if(!z)
		for(var/i in levels_by_trait(ZTRAIT_RESERVED))
			if(reserve.Reserve(width, height, i))
				return reserve
		//If we didn't return at this point, theres a good chance we ran out of room on the exisiting reserved z levels, so lets try a new one
		num_of_res_levels += 1
		var/datum/space_level/newReserved = add_new_zlevel("Transit/Reserved [num_of_res_levels]", list(ZTRAIT_RESERVED = TRUE))
		initialize_reserved_level(newReserved.z_value)
		if(reserve.Reserve(width, height, newReserved.z_value))
			return reserve
	else
		if(!level_trait(z, ZTRAIT_RESERVED))
			qdel(reserve)
			return
		else
			if(reserve.Reserve(width, height, z))
				return reserve
	QDEL_NULL(reserve)

//This is not for wiping reserved levels, use wipe_reservations() for that.
/datum/controller/subsystem/mapping/proc/initialize_reserved_level(z)
	UNTIL(!clearing_reserved_turfs)				//regardless, lets add a check just in case.
	clearing_reserved_turfs = TRUE			//This operation will likely clear any existing reservations, so lets make sure nothing tries to make one while we're doing it.
	if(!level_trait(z,ZTRAIT_RESERVED))
		clearing_reserved_turfs = FALSE
		CRASH("Invalid z level prepared for reservations.")
	var/turf/A = get_turf(locate(16, 16,z))
	var/turf/B = get_turf(locate(world.maxx - 16,world.maxy - 16,z))
	var/block = block(A, B)
	for(var/t in block)
		// No need to empty() these, because it's world init and they're
		// already /turf/open/space/basic.
		var/turf/T = t
		T.flags_1 |= UNUSED_RESERVATION_TURF_1
	unused_turfs["[z]"] = block
	reservation_ready["[z]"] = TRUE
	clearing_reserved_turfs = FALSE

/datum/controller/subsystem/mapping/proc/reserve_turfs(list/turfs)
	for(var/i in turfs)
		var/turf/T = i
		T.empty(RESERVED_TURF_TYPE, RESERVED_TURF_TYPE, null, TRUE)
		LAZYINITLIST(unused_turfs["[T.z]"])
		unused_turfs["[T.z]"] |= T
		T.flags_1 |= UNUSED_RESERVATION_TURF_1
		GLOB.areas_by_type[world.area].contents += T
		CHECK_TICK

//DO NOT CALL THIS PROC DIRECTLY, CALL wipe_reservations().
/datum/controller/subsystem/mapping/proc/do_wipe_turf_reservations()
	UNTIL(initialized)							//This proc is for AFTER init, before init turf reservations won't even exist and using this will likely break things.
	for(var/i in turf_reservations)
		var/datum/turf_reservation/TR = i
		if(!QDELETED(TR))
			qdel(TR, TRUE)
	UNSETEMPTY(turf_reservations)
	var/list/clearing = list()
	for(var/l in unused_turfs)			//unused_turfs is a assoc list by z = list(turfs)
		if(islist(unused_turfs[l]))
			clearing |= unused_turfs[l]
	clearing |= used_turfs		//used turfs is an associative list, BUT, reserve_turfs() can still handle it. If the code above works properly, this won't even be needed as the turfs would be freed already.
	unused_turfs.Cut()
	used_turfs.Cut()
	reserve_turfs(clearing)



/datum/controller/subsystem/mapping/proc/reg_in_areas_in_z(list/areas)
	for(var/B in areas)
		var/area/A = B
		A.reg_in_areas_in_z()

/datum/controller/subsystem/mapping/proc/get_isolated_ruin_z()
	if(!isolated_ruins_z)
		isolated_ruins_z = add_new_zlevel("Isolated Ruins/Reserved", list(ZTRAIT_RESERVED = TRUE, ZTRAIT_ISOLATED_RUINS = TRUE))
		initialize_reserved_level(isolated_ruins_z.z_value)
	return isolated_ruins_z.z_value


//The initialization of all our marks - this is what gets the ball rolling and self-deletes the marks after the maps are loaded
/datum/controller/subsystem/mapping/proc/load_marks()
	var/list/sites = SSmapping.map_load_marks

	if(!LAZYLEN(sites)) //This should never happen unless the base map failed to load or there are 0 marks on the map
		return

	for(var/M in sites) //Start it up
		var/obj/effect/landmark/map_load_mark/mark = M

		if(!LAZYLEN(mark.templates)) //Somehow our templates are empty
			continue

		var/datum/map_template/template = SSmapping.map_templates[pick(mark.templates)] //Find our actual existing template, it should be pre-loaded
		//Pick() should just randomly pick out of the templates list, or just grab the one there if there is only one
		if(istype(template)) //If our template pick failed, it should just abort and not do anything
			if(template.load(get_turf(mark))) //Fire it up. Should use bottom left corner.  This will take the majority of loading time
				LAZYREMOVE(SSmapping.map_load_marks,mark) //Get rid of the mark from our global list of marks
				qdel(mark) //Delete the mark now that the map is loaded
			else
				//Loading the template failed somehow (template.load returned a FALSE), did you spell the paths right?
				log_world("SSMapping: Failed to load template: [template.name] ([template.mappath])")

/datum/controller/subsystem/mapping/proc/add_world_trait(datum/world_trait/trait_type, duration = 30 MINUTES)
	var/datum/world_trait/new_trait = new trait_type
	active_world_traits |= new_trait

	if(duration > 0)
		addtimer(CALLBACK(src, PROC_REF(remove_world_trait), new_trait), duration)

/datum/controller/subsystem/mapping/proc/remove_world_trait(datum/world_trait/trait_to_remove)
	active_world_traits -= trait_to_remove
	qdel(trait_to_remove)

/datum/controller/subsystem/mapping/proc/find_and_remove_world_trait(datum/world_trait/trait_to_remove)
	for(var/datum/world_trait/trait in active_world_traits)
		if(!istype(trait, trait_to_remove))
			continue
		active_world_traits -= trait
		qdel(trait)
		return TRUE
	return FALSE

/proc/has_world_trait(datum/world_trait/trait_type)
	if(!length(SSmapping.active_world_traits))
		return FALSE
	for(var/datum/world_trait/trait in SSmapping.active_world_traits)
		if(!istype(trait, trait_type))
			continue
		return TRUE
	return FALSE

/proc/add_tracked_world_trait_atom(atom/incoming, datum/world_trait/trait_type)
	if(!length(SSmapping.active_world_traits))
		return FALSE
	for(var/datum/world_trait/trait in SSmapping.active_world_traits)
		if(!istype(trait, trait_type))
			continue
		trait.add_tracked(incoming)

/proc/remove_tracked_world_trait_atom(atom/removing, datum/world_trait/trait_type)
	if(!length(SSmapping.active_world_traits))
		return FALSE
	for(var/datum/world_trait/trait in SSmapping.active_world_traits)
		if(!istype(trait, trait_type))
			continue
		trait.remove_tracked(removing)
