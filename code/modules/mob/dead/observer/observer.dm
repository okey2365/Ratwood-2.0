GLOBAL_LIST_EMPTY(ghost_images_default) //this is a list of the default (non-accessorized, non-dir) images of the ghosts themselves
GLOBAL_LIST_EMPTY(ghost_images_simple) //this is a list of all ghost images as the simple white ghost

GLOBAL_VAR_INIT(observer_default_invisibility, INVISIBILITY_OBSERVER)

/mob/dead/observer
	name = "ghost"
	desc = "" //jinkies!
	icon = 'icons/mob/mob.dmi'
	icon_state = ""
	layer = GHOST_LAYER
	stat = DEAD
	density = FALSE
	see_invisible = SEE_INVISIBLE_OBSERVER
	see_in_dark = 100
	lighting_alpha = LIGHTING_PLANE_ALPHA_MOSTLY_INVISIBLE
	invisibility = INVISIBILITY_OBSERVER
	hud_type = /datum/hud/ghost
	movement_type = GROUND | FLYING
	var/draw_icon = FALSE
	var/can_reenter_corpse
	var/datum/hud/living/carbon/hud = null // hud
	var/bootime = 0
	var/started_as_observer //This variable is set to 1 when you enter the game as an observer.
							//If you died in the game and are a ghsot - this will remain as null.
							//Note that this is not a reliable way to determine if admins started as observers, since they change mobs a lot.
	var/atom/movable/following = null
	var/fun_verbs = 0
	var/image/ghostimage_default = null //this mobs ghost image without accessories and dirs
	var/image/ghostimage_simple = null //this mob with the simple white ghost sprite
	var/ghostvision = 1 //is the ghost able to see things humans can't?
	var/mob/observetarget = null	//The target mob that the ghost is observing. Used as a reference in logout()
	var/list/observed_screens	//Screen objects borrowed from observetarget, such as blindness overlays and alerts.
	var/ghost_hud_enabled = 1 //did this ghost disable the on-screen HUD?
	var/data_huds_on = 0 //Are data HUDs currently enabled?
	var/health_scan = FALSE //Are health scans currently enabled?
	var/gas_scan = FALSE //Are gas scans currently enabled?
	var/list/datahuds = list() //list of data HUDs shown to ghosts.
	var/ghost_orbit = GHOST_ORBIT_CIRCLE

	//These variables store hair data if the ghost originates from a species with head and/or facial hair.
	var/hairstyle
	var/hair_color
	var/mutable_appearance/hair_overlay
	var/facial_hairstyle
	var/facial_hair_color
	var/mutable_appearance/facial_hair_overlay
	var/ears
	var/mutable_appearance/ears_overlay

	var/updatedir = 1						//Do we have to update our dir as the ghost moves around?
	var/lastsetting = null	//Stores the last setting that ghost_others was set to, for a little more efficiency when we update ghost images. Null means no update is necessary

	//We store copies of the ghost display preferences locally so they can be referred to even if no client is connected.
	//If there's a bug with changing your ghost settings, it's probably related to this.
	var/ghost_accs = GHOST_ACCS_DEFAULT_OPTION
	var/ghost_others = GHOST_OTHERS_DEFAULT_OPTION
	// Used for displaying in ghost chat, without changing the actual name
	// of the mob
	var/deadchat_name
	var/datum/spawners_menu/spawners_menu
	var/datum/orbit_menu/orbit_menu
	var/orbiting_ref
	var/ghostize_time = 0
	move_resist = INFINITY

/mob/dead/observer/rogue
//	see_invisible = SEE_INVISIBLE_LIVING
	sight = 0
	see_in_dark = 8
	var/next_gmove
	var/misting = 0
	draw_icon = TRUE

/mob/dead/observer/admin
	hud_type = /datum/hud/adminghost

/mob/dead/observer/rogue/nodraw
	draw_icon = FALSE
	icon = 'icons/roguetown/mob/misc.dmi'
	icon_state = "hollow"
	alpha = 150

/mob/dead/observer/screye
//	see_invisible = SEE_INVISIBLE_LIVING
	sight = 0
	see_in_dark = 0
	hud_type = /datum/hud/obs

/mob/dead/observer/screye/blackmirror
	sight = SEE_TURFS | SEE_MOBS | SEE_OBJS
	see_in_dark = 100

/mob/dead/observer/screye/Move(n, direct)
	return



/mob/dead/observer/Initialize(mapload)
	set_invisibility(GLOB.observer_default_invisibility)

	verbs += list(
		/mob/dead/observer/proc/dead_tele,
		/mob/dead/observer/proc/open_spawners_menu,
		/mob/dead/observer/proc/tray_view)

	if(!istype(src, /mob/dead/observer/rogue/arcaneeye))
		if(!istype(src, /mob/dead/observer/screye))
			client?.verbs += GLOB.ghost_verbs
			to_chat(src, span_danger("Click the <b>SKULL</b> on the left of your HUD to respawn."))

	if(icon_state in GLOB.ghost_forms_with_directions_list)
		ghostimage_default = image(src.icon,src,src.icon_state + "")
	else
		ghostimage_default = image(src.icon,src,src.icon_state)
	ghostimage_default.override = TRUE
	GLOB.ghost_images_default |= ghostimage_default

	ghostimage_simple = image(src.icon,src,"")
	ghostimage_simple.override = TRUE
	GLOB.ghost_images_simple |= ghostimage_simple

	updateallghostimages()

	testing("BEGIN LOC [loc]")

	var/turf/T
	var/mob/body = loc
	if(ismob(body))
		T = get_turf(body)				//Where is the body located?
		testing("body [body] loc [body.loc]")
		if(!T)
			testing("no t yyy")
			if(istype(body, /mob/living/brain))
				var/obj/Y = body.loc
				testing("Y [Y] loc [Y.loc]")
				T = get_turf(Y)

		gender = body.gender
		if(body.mind && body.mind.name)
			if(body.mind.ghostname)
				name = body.mind.ghostname
			else
				name = body.mind.name
		else
			if(body.real_name)
				name = body.real_name
			else
				name = random_unique_name(gender)

		mind = body.mind	//we don't transfer the mind but we keep a reference to it.

		set_suicide(body.suiciding) // Transfer whether they committed suicide.

		if(draw_icon)
			if(ishuman(body))
//				var/mob/living/carbon/human/body_human = body
//				var/icon/out_icon = icon('icons/effects/effects.dmi', "nothing")
//				var/od = body_human.dir
//				for(var/D in GLOB.cardinals)
//					body_human.dir = D
//					COMPILE_OVERLAYS(body)
//					var/icon/partial = getFlatIcon(body, no_anim = TRUE, base_size = TRUE)
//					out_icon.Insert(partial,dir=D)
//				body_human.dir = od
				var/mutable_appearance/MA = new()
				MA.appearance = body
				MA.transform = null //so we are standing
				appearance = MA
				layer = GHOST_LAYER
				pixel_x = 0
				pixel_y = 0
				invisibility = INVISIBILITY_OBSERVER
//				icon = out_icon
				alpha = 100
/*			if(HAIR in body_human.dna.species.species_traits)
				hairstyle = body_human.hairstyle
				hair_color = brighten_color(body_human.hair_color)
			if(FACEHAIR in body_human.dna.species.species_traits)
				facial_hairstyle = body_human.facial_hairstyle
				facial_hair_color = brighten_color(body_human.facial_hair_color)
			*/
	update_icon()

	if(!T)
		testing("NO T")
		T = SSmapping.get_station_center()

	forceMove(T)

	if(!name)							//To prevent nameless ghosts
		name = random_unique_name(gender)
	real_name = name

	if(!fun_verbs)
		verbs -= /mob/dead/observer/verb/boo
		verbs -= /mob/dead/observer/verb/possess

	GLOB.dead_mob_list += src

	for(var/v in GLOB.active_alternate_appearances)
		if(!v)
			continue
		var/datum/atom_hud/alternate_appearance/AA = v
		AA.onNewMob(src)
	become_hearing_sensitive()
	. = ..()

	grant_all_languages()
//	show_data_huds()
//	data_huds_on = 1

/mob/dead/observer/Login()
	. = ..()
	if(!(istype(src, /mob/dead/observer/rogue/arcaneeye)))
		if(istype(src, /mob/dead/observer/screye))
			return
		client?.verbs += GLOB.ghost_verbs
		to_chat(src, span_danger("Click the <b>SKULL</b> on the left of your HUD to respawn."))

/mob/dead/observer/narsie_act()
	var/old_color = color
	color = "#960000"
	animate(src, color = old_color, time = 10, flags = ANIMATION_PARALLEL)
	addtimer(CALLBACK(src, TYPE_PROC_REF(/atom, update_atom_colour)), 10)

/mob/dead/observer/Destroy()
	clear_ghost_images(ghostimage_default, ghostimage_simple)
	GLOB.ghost_images_default -= ghostimage_default
	ghostimage_default = null

	GLOB.ghost_images_simple -= ghostimage_simple
	ghostimage_simple = null

	// updateallghostimages() // likely not necessary since we cleared them earlier

	STOP_PROCESSING(SShaunting, src)

	QDEL_NULL(spawners_menu)
	QDEL_NULL(orbit_menu)
	return ..()

/mob/dead/CanPass(atom/movable/mover, turf/target)
	return 1

/mob/dead/observer/rogue/CanPass(atom/movable/mover, turf/target)
	if(!isinhell)
		if(istype(mover, /mob/dead/observer/rogue))
			return 0
		if(istype(mover, /mob/dead/observer/rogue/arcaneeye))
			return 1
	return 1

/*
 * This proc will update the icon of the ghost itself, with hair overlays, as well as the ghost image.
 * Please call update_icon(icon_state) from now on when you want to update the icon_state of the ghost,
 * or you might end up with hair on a sprite that's not supposed to get it.
 * Hair will always update its dir, so if your sprite has no dirs the haircut will go all over the place.
 * |- Ricotez
 */
/mob/dead/observer/update_icon(new_form)
	. = ..()
/*
	if(client) //We update our preferences in case they changed right before update_icon was called.
		ghost_accs = client.prefs.ghost_accs
		ghost_others = client.prefs.ghost_others

	if(hair_overlay)
		cut_overlay(hair_overlay)
		hair_overlay = null

	if(facial_hair_overlay)
		cut_overlay(facial_hair_overlay)
		facial_hair_overlay = null

	if(ear_overlay)
		cut_overlay(ear_overlay)
		ear_overlay = null

	if(new_form)
		icon_state = new_form
		if(icon_state in GLOB.ghost_forms_with_directions_list)
			ghostimage_default.icon_state = new_form + "_nodir" //if this icon has dirs, the default ghostimage must use its nodir version or clients with the preference set to default sprites only will see the dirs
		else
			ghostimage_default.icon_state = new_form

	if(ghost_accs >= GHOST_ACCS_DIR && icon_state in GLOB.ghost_forms_with_directions_list) //if this icon has dirs AND the client wants to show them, we make sure we update the dir on movement
		updatedir = 1
	else
		updatedir = 0	//stop updating the dir in case we want to show accessories with dirs on a ghost sprite without dirs
		setDir(2 		)//reset the dir to its default so the sprites all properly align up

	if(ghost_accs == GHOST_ACCS_FULL && icon_state in GLOB.ghost_forms_with_accessories_list) //check if this form supports accessories and if the client wants to show them
		var/datum/sprite_accessory/S
		if(facial_hairstyle)
			S = GLOB.facial_hairstyles_list[facial_hairstyle]
			if(S)
				facial_hair_overlay = mutable_appearance(S.icon, "[S.icon_state]", -HAIR_LAYER)
				if(facial_hair_color)
					facial_hair_overlay.color = "#" + facial_hair_color
				facial_hair_overlay.alpha = 200
				add_overlay(facial_hair_overlay)
		if(hairstyle)
			S = GLOB.hairstyles_list[hairstyle]
			if(S)
				hair_overlay = mutable_appearance(S.icon, "[S.icon_state]", -HAIR_LAYER)
				if(hair_color)
					hair_overlay.color = "#" + hair_color
				hair_overlay.alpha = 200
				add_overlay(hair_overlay)
		if(ear_style)
			S = GLOB.ears_list[ear_style]
			ear_overlay = mutable_appearance(S.icon, layer = -layer)*/


/*
 * Increase the brightness of a color by calculating the average distance between the R, G and B values,
 * and maximum brightness, then adding 30% of that average to R, G and B.
 *
 * I'll make this proc global and move it to its own file in a future update. |- Ricotez
 */
/mob/proc/brighten_color(input_color)
	var/r_val
	var/b_val
	var/g_val
	var/color_format = length(input_color)
	if(color_format == 3)
		r_val = hex2num(copytext(input_color, 1, 2))*16
		g_val = hex2num(copytext(input_color, 2, 3))*16
		b_val = hex2num(copytext(input_color, 3, 0))*16
	else if(color_format == 6)
		r_val = hex2num(copytext(input_color, 1, 3))
		g_val = hex2num(copytext(input_color, 3, 5))
		b_val = hex2num(copytext(input_color, 5, 0))
	else
		return 0 //If the color format is not 3 or 6, you're using an unexpected way to represent a color.

	r_val += (255 - r_val) * 0.4
	if(r_val > 255)
		r_val = 255
	g_val += (255 - g_val) * 0.4
	if(g_val > 255)
		g_val = 255
	b_val += (255 - b_val) * 0.4
	if(b_val > 255)
		b_val = 255

	return num2hex(r_val, 2) + num2hex(g_val, 2) + num2hex(b_val, 2)

/*
Transfer_mind is there to check if mob is being deleted/not going to have a body.
Works together with spawning an observer, noted above.
*/

/mob/proc/ghostize(can_reenter_corpse = 1, force_respawn = FALSE, admin = FALSE, drawskip)
	if(!key)
		return
	stop_sound_channel(CHANNEL_HEARTBEAT) //Stop heartbeat sounds because You Are A Ghost Now
//	stop_all_loops()
	if(client)
		SSdroning.kill_rain(client)
		SSdroning.kill_loop(client)
		SSdroning.kill_droning(client)
//		var/S = sound('sound/ambience/creepywind.ogg', repeat = 1, wait = 0, volume = client.prefs.musicvol, channel = CHANNEL_MUSIC)
//		play_priomusic(S)
	var/mob/dead/observer/ghost	// Transfer safety to observer spawning proc.
	if(admin)
		ghost = new /mob/dead/observer/admin(src)
	else if(drawskip)
		ghost = new /mob/dead/observer/rogue/nodraw(src)
	else
		ghost = new /mob/dead/observer/rogue(src)
	if(!admin)
		ghost.add_client_colour(/datum/client_colour/monochrome)
	ghost.ghostize_time = world.time
	SStgui.on_transfer(src, ghost) // Transfer NanoUIs.
	ghost.can_reenter_corpse = can_reenter_corpse
	ghost.key = key
	return ghost

/mob/living/carbon/human/ghostize(can_reenter_corpse = 1, force_respawn = FALSE, admin = FALSE, drawskip = FALSE)
	if(mind)
		if(mind.has_antag_datum(/datum/antagonist/zombie))
			if(force_respawn)
				mind.remove_antag_datum(/datum/antagonist/zombie)
				return ..()
			var/datum/antagonist/zombie/Z = mind.has_antag_datum(/datum/antagonist/zombie)
			if(!Z.revived)
				if(!(world.time % 5))
					to_chat(src, span_warning("I'm preparing to walk again."))
				return
	return ..()

/mob/proc/scry_ghost()
	if(key)
		stop_sound_channel(CHANNEL_HEARTBEAT) //Stop heartbeat sounds because You Are A Ghost Now
//		stop_all_loops()
		if(client)
			SSdroning.kill_rain(client)
			SSdroning.kill_loop(client)
			SSdroning.kill_droning(client)
		var/mob/dead/observer/screye/ghost = new(src)	// Transfer safety to observer spawning proc.
		ghost.ghostize_time = world.time
		SStgui.on_transfer(src, ghost) // Transfer NanoUIs.
		ghost.can_reenter_corpse = TRUE
		ghost.key = key
		return ghost

/*
This is the proc mobs get to turn into a ghost. Forked from ghostize due to compatibility issues.
*/
/mob/living/verb/ghost()
	set category = "OOC"
	set name = "Ghost"
	set desc = ""
	set hidden = 1
	if(!usr.client.holder)
		return
	if(stat != DEAD)
		succumb()
	if(stat == DEAD)
		ghostize(1)
	else
		var/response = alert(src, "Are you -sure- you want to ghost?\n(You are alive. If you ghost whilst still alive you may not play again this round! You can't change your mind so choose wisely!!)","Are you sure you want to ghost?","Ghost","Stay in body")
		if(response != "Ghost")
			return	//didn't want to ghost after-all
		ghostize(0)						//0 parameter is so we can never re-enter our body, "Charlie, you can never come baaaack~" :3

/mob/camera/verb/ghost()
	set category = "OOC"
	set name = "Ghost"
	set desc = ""
	set hidden = 1
	if(!usr.client.holder)
		return
	var/response = alert(src, "Are you -sure- you want to ghost?\n(You are alive. If you ghost whilst still alive you may not play again this round! You can't change your mind so choose wisely!!)","Are you sure you want to ghost?","Ghost","Stay in body")
	if(response != "Ghost")
		return
	ghostize(0)

/mob/dead/observer/Move(NewLoc, direct)
	if(observetarget) //Moving away gives us our own eyes back.
		reset_perspective(null)
	if(updatedir)
		setDir(direct)//only update dir if we actually need it, so overlays won't spin on base sprites that don't have directions of their own
	var/oldloc = loc

	if(NewLoc)
		forceMove(NewLoc)
		update_parallax_contents()
	else
		forceMove(get_turf(src))  //Get out of closets and such as a ghost
		if((direct & NORTH) && y < world.maxy)
			y++
		else if((direct & SOUTH) && y > 1)
			y--
		if((direct & EAST) && x < world.maxx)
			x++
		else if((direct & WEST) && x > 1)
			x--

	Moved(oldloc, direct)

/mob/dead/observer/proc/reenter_corpse()
	set category = "Ghost"
	set name = "Re-enter Corpse"
	set hidden = 1
	if(!client)
		return
	if(!mind || QDELETED(mind.current))
		to_chat(src, span_warning("I have no body."))
		return
	if(!can_reenter_corpse)
		to_chat(src, span_warning("I cannot re-enter my body."))
		return
	if(mind.current.key && copytext(mind.current.key,1,2)!="@")	//makes sure we don't accidentally kick any clients
		to_chat(usr, span_warning("Another consciousness is in my body... It is resisting me."))
		return
//	stop_all_loops()
	SSdroning.kill_rain(src.client)
	SSdroning.kill_loop(src.client)
	SSdroning.kill_droning(src.client)
	remove_client_colour(/datum/client_colour/monochrome)
	client.change_view(CONFIG_GET(string/default_view))
	client?.verbs -= GLOB.ghost_verbs
	SStgui.on_transfer(src, mind.current) // Transfer NanoUIs.
	mind.current.key = key
	return TRUE

/mob/dead/observer/returntolobby(modifier as num)
	set name = "{RETURN TO LOBBY}"
	set category = "Options"
	set hidden = 1
	if (CONFIG_GET(flag/norespawn))
		return
	if ((stat != DEAD || !( SSticker )))
		to_chat(src, span_boldnotice("I must be dead to use this!"))
		return

//	if(mind?.current && (world.time < mind.current.timeofdeath + RESPAWNTIME))
//		to_chat(usr, span_warning("I can return in [mind.current.timeofdeath + RESPAWNTIME - world.time]."))
//		return

	if(key)
		if(modifier)
			GLOB.respawntimes[key] = world.time + modifier
		else
			GLOB.respawntimes[key] = world.time

	log_game("[key_name(src)] used abandon mob.")

	to_chat(src, span_info("Returned to lobby successfully."))

	if(!client)
		log_game("[key_name(src)] AM failed due to disconnect.")
		return
	client.screen.Cut()
	client.screen += client.void
//	stop_all_loops()
	SSdroning.kill_rain(src.client)
	SSdroning.kill_loop(src.client)
	SSdroning.kill_droning(src.client)
	remove_client_colour(/datum/client_colour/monochrome)
	if(!client)
		log_game("[key_name(src)] AM failed due to disconnect.")
		return

	var/mob/dead/new_player/M = new /mob/dead/new_player()
	if(!client)
		log_game("[key_name(src)] AM failed due to disconnect.")
		qdel(M)
		return

	client.verbs -= GLOB.ghost_verbs
	M.key = key
	return


/mob/dead/observer/verb/stay_dead()
	set category = "Ghost"
	set name = "Do Not Resuscitate"
	set hidden = 1
	if(!check_rights(R_WATCH))
		return
	if(!client)
		return
	if(!can_reenter_corpse)
		to_chat(usr, span_warning("You're already stuck out of your body!"))
		return FALSE

	var/response = alert(src, "Are you sure you want to prevent (almost) all means of resuscitation? This cannot be undone. ","Are you sure you want to stay dead?","DNR","Save Me")
	if(response != "DNR")
		return

	can_reenter_corpse = FALSE
	to_chat(src, span_boldnotice("I can no longer be brought back into your body."))
	return TRUE

/mob/dead/observer/proc/notify_cloning(message, sound, atom/source, flashwindow = TRUE)
	if(flashwindow)
		window_flash(client)
	if(message)
		to_chat(src, span_ghostalert("[message]"))
		if(source)
			var/atom/movable/screen/alert/A = throw_alert("[REF(source)]_notify_cloning", /atom/movable/screen/alert/notify_cloning)
			if(A)
				if(client && client.prefs && client.prefs.UI_style)
					A.icon = ui_style2icon(client.prefs.UI_style)
				A.desc = message
				var/old_layer = source.layer
				var/old_plane = source.plane
				source.layer = FLOAT_LAYER
				source.plane = FLOAT_PLANE
				A.add_overlay(source)
				source.layer = old_layer
				source.plane = old_plane
	to_chat(src, span_ghostalert("<a href=?src=[REF(src)];reenter=1>(Click to re-enter)</a>"))
	if(sound)
		SEND_SOUND(src, sound(sound))

/mob/dead/observer/proc/dead_tele()
	set category = "Ghost"
	set name = "Teleport"
	set desc= "Teleport to a location"
	set hidden = 1
	if(!check_rights(R_WATCH))
		return
	if(!isobserver(usr))
		to_chat(usr, span_warning("Not when you're not dead!"))
		return
	var/list/filtered = list()
	for(var/V in GLOB.sortedAreas)
		var/area/A = V
		if(!A.hidden)
			filtered += A
	var/area/thearea  = input("Area to jump to", "BOOYEA") as null|anything in filtered

	if(!thearea)
		return

	var/list/L = list()
	for(var/turf/T in get_area_turfs(thearea.type))
		L+=T

	if(!L || !L.len)
		to_chat(usr, span_warning("No area available."))
		return

	usr.forceMove(pick(L))
	update_parallax_contents()

/mob/dead/observer/verb/follow()
	set category = "Ghost"
	set name = "Orbit" // "Haunt"
	set desc = ""
	set hidden = 1
	var/list/all_mobs = getpois(mobs_only=1,skip_mindless=1)
	var/list/allowed_mobs = list()

	// admins can see everybody, i think thats fair
	if(!check_rights(R_ADMIN, FALSE))
		for(var/current_name in all_mobs)
			var/mob/current_mob = all_mobs[current_name]

			if(current_mob.client)
				// check if the player is has ghost protection
				var/datum/preferences/current_prefs = current_mob.client.prefs
				if(!current_prefs.ghost_protection)
					allowed_mobs[current_name] = current_mob
	else
		allowed_mobs += all_mobs

	var/input = input("Who?!", "Haunt", null, null) as null|anything in allowed_mobs
	var/mob/target = allowed_mobs[input]

	ManualFollow(target)

/datum/mind
	var/list/attackedme = list()

// This is the ghost's follow verb with an argument
/mob/dead/observer/proc/ManualFollow(atom/movable/target)
	if (!istype(target))
		return
	if(is_hidden_from_ghosts(target, src))
		return

	var/icon/I = icon(target.icon,target.icon_state,target.dir)

	var/orbitsize = (I.Width()+I.Height())*0.5
	orbitsize -= (orbitsize/world.icon_size)*(world.icon_size*0.25)

	var/rot_seg

	switch(ghost_orbit)
		if(GHOST_ORBIT_TRIANGLE)
			rot_seg = 3
		if(GHOST_ORBIT_SQUARE)
			rot_seg = 4
		if(GHOST_ORBIT_PENTAGON)
			rot_seg = 5
		if(GHOST_ORBIT_HEXAGON)
			rot_seg = 6
		else //Circular
			rot_seg = 36 //360/10 bby, smooth enough aproximation of a circle

	//A ghost copies the appearance of a body. KEEP_TOGETHER then moves the spin center onto the overlays, so orbit by pixel.
	orbit(target, orbitsize, FALSE, 20, rot_seg, FALSE, TRUE) //no pre_rotation, pixel_orbit
	orbiting_ref = REF(target)

/mob/dead/observer/orbit()
	setDir(2)//reset dir so the right directional sprites show up
	return ..()

/mob/dead/observer/stop_orbit(datum/component/orbiter/orbits)
	. = ..()
	orbiting_ref = null
	//restart our floating animation after orbit is done.
	pixel_y = 0
	pixel_x = 0
	animate(src, pixel_y = 2, time = 10, loop = -1)
	if(observetarget) //Moving ends the orbit, and the end of an orbit gives us our own eyes back.
		reset_perspective(null)

/mob/dead/observer/verb/jumptomob() //Moves the ghost instead of just changing the ghosts's eye -Nodrak
	set category = "Ghost"
	set name = "Jump to Mob"
	set desc = ""
	set hidden = 1
	if(!check_rights(R_WATCH))
		return
	if(isobserver(usr)) //Make sure they're an observer!


		var/list/dest = list() //List of possible destinations (mobs)
		var/target = null	   //Chosen target.

		dest += getpois(mobs_only=1) //Fill list, prompt user with list
		target = input("Please, select a player!", "Jump to Mob", null, null) as null|anything in dest

		if (!target)//Make sure we actually have a target
			return
		else
			var/mob/M = dest[target] //Destination mob
			var/mob/A = src			 //Source mob
			var/turf/T = get_turf(M) //Turf of the destination mob

			if(T && isturf(T))	//Make sure the turf exists, then move the source to that destination.
				A.forceMove(T)
				A.update_parallax_contents()
			else
				to_chat(A, span_danger("This mob is not located in the game world."))

/mob/dead/observer/verb/change_view_range()
	set category = "Ghost"
	set name = "View Range"
	set desc = ""
	set hidden = 1
	if(!check_rights(R_DEBUG))
		return
	var/max_view = client.prefs.unlock_content ? GHOST_MAX_VIEW_RANGE_MEMBER : GHOST_MAX_VIEW_RANGE_DEFAULT
	if(client.view == CONFIG_GET(string/default_view))
		var/list/views = list()
		for(var/i in 7 to max_view)
			views |= i
		var/new_view = input("Choose your new view", "Modify view range", 7) as null|anything in views
		if(new_view)
			client.change_view(CLAMP(new_view, 7, max_view))
	else
		client.change_view(CONFIG_GET(string/default_view))

/mob/dead/observer/verb/add_view_range(input as num)
	set name = "Add View Range"
	set hidden = TRUE
	var/max_view = client.prefs.unlock_content ? GHOST_MAX_VIEW_RANGE_MEMBER : GHOST_MAX_VIEW_RANGE_DEFAULT
	if(input)
		client.rescale_view(input, 15, (max_view*2)+1)

/mob/dead/observer/verb/boo()
	set category = "Ghost"
	set name = "Boo!"
	set desc= "Scare your crew members because of boredom!"

	if(bootime > world.time)
		return
	var/obj/machinery/light/L = locate(/obj/machinery/light) in view(1, src)
	if(L)
		L.flicker()
		bootime = world.time + 600
		return
	//Maybe in the future we can add more <i>spooky</i> code here!
	return


/mob/dead/observer/memory()
	set hidden = 1
	to_chat(src, span_danger("I are dead! You have no mind to store memory!"))

/mob/dead/observer/add_memory()
	set hidden = 1
	to_chat(src, span_danger("I are dead! You have no mind to store memory!"))

/mob/dead/observer/verb/toggle_ghostsee()
	set name = "Toggle Ghost Vision"
	set desc = ""
	set category = "Ghost"
	set hidden = 1
	ghostvision = !(ghostvision)
	update_sight()
	to_chat(usr, span_boldnotice("I [(ghostvision?"now":"no longer")] have ghost vision."))

/mob/dead/observer/verb/toggle_darkness()
	set name = "Toggle Darkness"
	set category = "Ghost"
	set hidden = 1
	switch(lighting_alpha)
		if (LIGHTING_PLANE_ALPHA_VISIBLE)
			lighting_alpha = LIGHTING_PLANE_ALPHA_MOSTLY_VISIBLE
		if (LIGHTING_PLANE_ALPHA_MOSTLY_VISIBLE)
			lighting_alpha = LIGHTING_PLANE_ALPHA_MOSTLY_INVISIBLE
		if (LIGHTING_PLANE_ALPHA_MOSTLY_INVISIBLE)
			lighting_alpha = LIGHTING_PLANE_ALPHA_INVISIBLE
		else
			lighting_alpha = LIGHTING_PLANE_ALPHA_VISIBLE

	update_sight()

/mob/dead/observer/update_sight()
	if(client)
		ghost_others = client.prefs.ghost_others //A quick update just in case this setting was changed right before calling the proc

	if(!isnull(observetarget)) //Borrowed eyes. We see the dark exactly like the mob we observe.
		sight = observetarget.sight
		see_in_dark = observetarget.see_in_dark
		see_invisible = observetarget.see_invisible
		lighting_alpha = observetarget.lighting_alpha
	else if (!ghostvision)
		see_invisible = SEE_INVISIBLE_LIVING
	else
		see_invisible = SEE_INVISIBLE_OBSERVER


	updateghostimages()
	..()

/// Clears the two provided images from all observers.
/proc/clear_ghost_images(image/default_image, image/simple_image)
	for (var/mob/dead/observer/O in GLOB.player_list)
		if(!O.ghostvision)
			continue
		if(O.client)
			O.client.images -= default_image
			O.client.images -= simple_image

/proc/updateallghostimages()
	listclearnulls(GLOB.ghost_images_default)
	listclearnulls(GLOB.ghost_images_simple)

	for (var/mob/dead/observer/O in GLOB.player_list)
		O.updateghostimages()

/mob/dead/observer/proc/horde_respawn()
	if(istype(src, /mob/dead/observer/rogue/arcaneeye))
		return
	var/bt = world.time
	SEND_SOUND(src, sound('sound/misc/notice (2).ogg'))
	if(alert(src, "You have been summoned you to destroy!", "Join the Horde", "Yes", "No") == "Yes")
		if(world.time > bt + 5 MINUTES)
			to_chat(src, span_warning("Too late."))
			return FALSE
		returntolobby(RESPAWNTIME*-1)


/mob/dead/observer/proc/updateghostimages()
	if (!client)
		return

	if(lastsetting)
		switch(lastsetting) //checks the setting we last came from, for a little efficiency so we don't try to delete images from the client that it doesn't have anyway
			if(GHOST_OTHERS_DEFAULT_SPRITE)
				client.images -= GLOB.ghost_images_default
			if(GHOST_OTHERS_SIMPLE)
				client.images -= GLOB.ghost_images_simple
	lastsetting = client.prefs.ghost_others
	if(!ghostvision)
		return
	if(client.prefs.ghost_others != GHOST_OTHERS_THEIR_SETTING)
		switch(client.prefs.ghost_others)
			if(GHOST_OTHERS_DEFAULT_SPRITE)
				client.images |= (GLOB.ghost_images_default-ghostimage_default)
			if(GHOST_OTHERS_SIMPLE)
				client.images |= (GLOB.ghost_images_simple-ghostimage_simple)

/mob/dead/observer/verb/possess()
	set category = "Ghost"
	set name = "Possess!"
	set desc= "Take over the body of a mindless creature!"

	var/list/possessible = list()
	for(var/mob/living/L in GLOB.alive_mob_list)
		if(istype(L,/mob/living/carbon/human/dummy) || !get_turf(L)) //Haha no.
			continue
		if(!(L in GLOB.player_list) && !L.mind)
			possessible += L

	var/mob/living/target = input("Your new life begins today!", "Possess Mob", null, null) as null|anything in sortNames(possessible)

	if(!target)
		return FALSE

	if(can_reenter_corpse && mind?.current)
		if(alert(src, "Your soul is still tied to your former life as [mind.current.name], if you go forward there is no going back to that life. Are you sure you wish to continue?", "Move On", "Yes", "No") == "No")
			return FALSE
	if(target.key)
		to_chat(src, span_warning("Someone has taken this body while you were choosing!"))
		return FALSE

	target.key = key
	target.faction = list("neutral")
	return TRUE

//this is a mob verb instead of atom for performance reasons
//see /mob/verb/examinate() in mob.dm for more info
//overridden here and in /mob/living for different point span classes and sanity checks
/mob/dead/observer/pointed(atom/A as mob|obj|turf in view(client.view, src))
	if(!..())
		return FALSE
	usr.visible_message(span_deadsay("<b>[src]</b> points to [A]."))
	return TRUE

/mob/dead/observer/verb/view_manifest()
	set name = "View Crew Manifest"
	set category = "Ghost"
	set hidden = 1
	if(!check_rights(R_WATCH))
		return
	var/dat
	dat += "<h4>Crew Manifest</h4>"
	dat += GLOB.data_core.get_manifest()

	src << browse(dat, "window=manifest;size=387x420;can_close=1")

//this is called when a ghost is drag clicked to something.
/mob/dead/observer/MouseDrop(atom/over)
	if(!usr || !over)
		return
	if (isobserver(usr) && usr.client.holder && (isliving(over) || iscameramob(over)) )
		if (usr.client.holder.cmd_ghost_drag(src,over))
			return

	return ..()

/mob/dead/observer/Topic(href, href_list)
	..()
	if(usr == src)
		if(href_list["follow"])
			var/atom/movable/target = locate(href_list["follow"])
			if(istype(target) && (target != src))
				ManualFollow(target)
				return
		if(href_list["x"] && href_list["y"] && href_list["z"])
			var/tx = text2num(href_list["x"])
			var/ty = text2num(href_list["y"])
			var/tz = text2num(href_list["z"])
			var/turf/target = locate(tx, ty, tz)
			if(istype(target))
				forceMove(target)
				return
		if(href_list["reenter"])
			reenter_corpse()
			return

//We don't want to update the current var
//But we will still carry a mind.
/mob/dead/observer/mind_initialize()
	return

/mob/dead/observer/proc/show_data_huds()
	for(var/hudtype in datahuds)
		var/datum/atom_hud/H = GLOB.huds[hudtype]
		H.add_hud_to(src)

/mob/dead/observer/proc/remove_data_huds()
	for(var/hudtype in datahuds)
		var/datum/atom_hud/H = GLOB.huds[hudtype]
		H.remove_hud_from(src)

/mob/dead/observer/verb/toggle_data_huds()
	set name = "Toggle Sec/Med/Diag HUD"
	set desc = ""
	set category = "Ghost"
	set hidden = 1
	if(!check_rights(R_WATCH))
		return
	if(data_huds_on) //remove old huds
		remove_data_huds()
		to_chat(src, span_notice("Data HUDs disabled."))
		data_huds_on = 0
	else
		show_data_huds()
		to_chat(src, span_notice("Data HUDs enabled."))
		data_huds_on = 1

/mob/dead/observer/verb/toggle_health_scan()
	set name = "Toggle Health Scan"
	set desc = ""
	set category = "Ghost"
	set hidden = 1
	if(!check_rights(R_WATCH))
		return
	if(health_scan) //remove old huds
		to_chat(src, span_notice("Health scan disabled."))
		health_scan = FALSE
	else
		to_chat(src, span_notice("Health scan enabled."))
		health_scan = TRUE

/mob/dead/observer/verb/toggle_gas_scan()
	set name = "Toggle Gas Scan"
	set desc = ""
	set category = "Ghost"
	set hidden = 1
	if(!check_rights(R_WATCH))
		return
	if(gas_scan)
		to_chat(src, span_notice("Gas scan disabled."))
		gas_scan = FALSE
	else
		to_chat(src, span_notice("Gas scan enabled."))
		gas_scan = TRUE

/mob/dead/observer/verb/restore_ghost_appearance()
	set name = "Restore Ghost Character"
	set desc = "Sets your deadchat name and ghost appearance to your \
		roundstart character."
	set category = "Ghost"
	set hidden = 1
	if(!check_rights(R_WATCH))
		return
	set_ghost_appearance()
	if(client && client.prefs)
		deadchat_name = client.prefs.real_name
		mind.ghostname = client.prefs.real_name
		name = client.prefs.real_name

/mob/dead/observer/proc/set_ghost_appearance()
	if((!client) || (!client.prefs))
		return

	if(client.prefs.randomise[RANDOM_NAME])
		client.prefs.real_name = random_unique_name(gender)
	if(client.prefs.randomise[RANDOM_BODY])
		client.prefs.random_character(gender)

	if(HAIR in client.prefs.pref_species.species_traits)
		hairstyle = client.prefs.hairstyle
		hair_color = brighten_color(client.prefs.hair_color)
	if(FACEHAIR in client.prefs.pref_species.species_traits)
		facial_hairstyle = client.prefs.facial_hairstyle
		facial_hair_color = brighten_color(client.prefs.facial_hair_color)

	update_icon()

/mob/dead/observer/canUseTopic(atom/movable/M, be_close=FALSE, no_dexterity=FALSE, no_tk=FALSE)
	return IsAdminGhost(usr)

/mob/dead/observer/is_literate()
	return TRUE

/mob/dead/observer/vv_edit_var(var_name, var_value)
	. = ..()
	switch(var_name)
		if("icon")
			ghostimage_default.icon = icon
			ghostimage_simple.icon = icon
		if("icon_state")
			ghostimage_default.icon_state = icon_state
			ghostimage_simple.icon_state = icon_state
		if("fun_verbs")
			if(fun_verbs)
				verbs += /mob/dead/observer/verb/boo
				verbs += /mob/dead/observer/verb/possess
			else
				verbs -= /mob/dead/observer/verb/boo
				verbs -= /mob/dead/observer/verb/possess

/mob/dead/observer/reset_perspective(atom/A)
	cleanup_observe()
	if(..())
		if(hud_used)
			client.screen = list()
			hud_used.show_hud(hud_used.hud_version)

/// Look through the eyes of another mob. Copies the HUD, the sight and the darkness.
/mob/dead/observer/proc/do_observe(mob/mob_eye)
	if(!client || !istype(mob_eye) || mob_eye == src)
		return FALSE
	if(istype(mob_eye, /mob/dead/new_player))
		return FALSE
	if(mob_eye == observetarget) //A second look at the same mob gives our own eyes back.
		reset_perspective(null)
		return TRUE
	if(is_hidden_from_ghosts(mob_eye, src))
		to_chat(src, span_warning("That one is hidden from me."))
		return FALSE
	//Two ghosts must never watch each other. A sight update would then bounce between them forever.
	var/mob/chain = mob_eye
	while(isobserver(chain))
		var/mob/dead/observer/eyes = chain
		if(eyes == src)
			return FALSE
		chain = eyes.observetarget

	reset_perspective(null)

	client.eye = mob_eye
	client.perspective = EYE_PERSPECTIVE
	observetarget = mob_eye
	if(mob_eye.hud_used)
		client.screen = list()
		LAZYINITLIST(mob_eye.observers)
		mob_eye.observers |= src
		mob_eye.hud_used.show_hud(mob_eye.hud_used.hud_version, src)
	RegisterSignal(mob_eye, COMSIG_MOB_UPDATE_SIGHT, PROC_REF(on_observed_sight_change))
	sync_observed_vision()
	to_chat(src, span_notice("I see through [mob_eye]'s eyes. Orbit or follow something else to stop."))
	return TRUE

/// Stop observing and restore our own eyes.
/mob/dead/observer/proc/cleanup_observe()
	var/mob/target = observetarget
	if(isnull(target))
		return
	observetarget = null
	UnregisterSignal(target, COMSIG_MOB_UPDATE_SIGHT)
	if(target.observers)
		target.observers -= src
		UNSETEMPTY(target.observers)
	clear_observed_screens()
	sight = initial(sight)
	see_in_dark = initial(see_in_dark)
	see_invisible = initial(see_invisible)
	lighting_alpha = initial(lighting_alpha)
	update_sight()
	hud_used?.plane_masters_update() //Take our own planes back.

/mob/dead/observer/proc/on_observed_sight_change(datum/source)
	SIGNAL_HANDLER
	sync_observed_vision()

/// Copy the eyesight of the observed mob: darkness, vision flags, screen overlays and alerts.
/mob/dead/observer/proc/sync_observed_vision()
	if(isnull(observetarget))
		return
	update_sight()
	sync_observed_screens()

/// Take the whole screen of the observed mob. Later changes arrive through push_screen_to_observers().
/mob/dead/observer/proc/sync_observed_screens()
	var/mob/target = observetarget
	if(isnull(target) || !client)
		return
	clear_observed_screens()
	for(var/category in target.screens)
		add_observed_screen(target.screens[category])
	for(var/category in target.alerts)
		add_observed_screen(target.alerts[category])
	//Whatever else their client draws, such as the vision relays of special eyes.
	for(var/atom/movable/screen_object as anything in target.client?.screen)
		add_observed_screen(screen_object)
	client.color = target.client?.color || ""
	hud_used?.plane_masters_update()

/// Trade our planes for the ones of the mob we observe. We then render the world the way their eyes do.
/mob/dead/observer/proc/sync_observed_planes()
	var/datum/hud/target_hud = observetarget?.hud_used
	if(!target_hud || !client)
		return
	for(var/thing in target_hud.plane_masters)
		add_observed_screen(target_hud.plane_masters[thing])

/mob/dead/observer/proc/add_observed_screen(atom/movable/screen_object)
	if(!screen_object || !client)
		return
	LAZYOR(observed_screens, screen_object)
	client.screen |= screen_object

/mob/dead/observer/proc/remove_observed_screen(atom/movable/screen_object)
	if(!screen_object)
		return
	LAZYREMOVE(observed_screens, screen_object)
	if(client)
		client.screen -= screen_object

/mob/dead/observer/proc/clear_observed_screens()
	if(client)
		for(var/atom/movable/screen_object as anything in observed_screens)
			client.screen -= screen_object
		client.color = ""
	observed_screens = null

/// Draw a screen object on our client and on every ghost that watches us. Use this instead of client.screen += object.
/mob/proc/add_screen_object(atom/movable/screen_object)
	if(!screen_object)
		return
	client?.screen |= screen_object
	push_screen_to_observers(screen_object)

/// Take a screen object off our client and off every ghost that watches us.
/mob/proc/remove_screen_object(atom/movable/screen_object)
	if(!screen_object)
		return
	client?.screen -= screen_object
	push_screen_to_observers(screen_object, TRUE)

/// Send a screen change to every ghost that watches our HUD, such as a blindness overlay or an alert.
/mob/proc/push_screen_to_observers(atom/movable/screen_object, remove = FALSE)
	if(!screen_object || !length(observers))
		return
	for(var/mob/dead/observer/watcher as anything in observers)
		if(watcher.observetarget != src)
			continue
		if(remove)
			watcher.remove_observed_screen(screen_object)
		else
			watcher.add_observed_screen(screen_object)

/// Give every ghost that watches our HUD the same screen color.
/mob/proc/push_client_colour_to_observers()
	if(!length(observers))
		return
	for(var/mob/dead/observer/watcher as anything in observers)
		if(watcher.observetarget != src || !watcher.client)
			continue
		watcher.client.color = client?.color || ""

/mob/dead/observer/verb/observe()
	set name = "Observe"
	set category = "OOC"
	set hidden = 1
	if(!check_rights(R_WATCH))
		return
	var/list/creatures = getpois()

	reset_perspective(null)

	var/eye_name = input("Please, select a player!", "Observe", null, null) as null|anything in creatures

	if (!eye_name)
		return

	var/mob/mob_eye = creatures[eye_name]
	ManualFollow(mob_eye) //Orbit them as well, so moving away drops the borrowed eyes.
	do_observe(mob_eye)

/mob/dead/observer/CtrlShiftClick(mob/user)
	if(isobserver(user) && check_rights(R_SPAWN))
		change_mob_type( /mob/living/carbon/human , null, null, TRUE) //always delmob, ghosts shouldn't be left lingering

/mob/dead/observer/examine(mob/user)
	. = ..()
	if(!invisibility)
		. += "It seems extremely obvious."

/mob/dead/observer/proc/set_invisibility(value)
	invisibility = value
	if(!value)
		set_light(1, 1, 2)
	else
		set_light(0, 0, 0)

/mob/dead/observer/vv_edit_var(var_name, var_value)
	. = ..()
	if(var_name == "invisibility")
		set_invisibility(invisibility) // updates light

/proc/set_observer_default_invisibility(amount, message=null)
	for(var/mob/dead/observer/G in GLOB.player_list)
		G.set_invisibility(amount)
		if(message)
			to_chat(G, message)
	GLOB.observer_default_invisibility = amount

/mob/dead/observer/proc/open_spawners_menu()
	set name = "Spawners Menu"
	set desc = ""
	set category = "Ghost"
	set hidden = 1
	if(!check_rights(R_DEBUG))
		return
	if(!spawners_menu)
		spawners_menu = new(src)

	spawners_menu.ui_interact(src)

/mob/dead/observer/proc/open_orbit_menu()
	set name = "Orbit"
	set desc = ""
	set category = "Ghost"
	set hidden = 1
	if(!orbit_menu)
		orbit_menu = new(src)

	orbit_menu.ui_interact(src)

/mob/dead/observer/proc/tray_view()
	set category = "Ghost"
	set name = "T-ray view"
	set desc = ""
	set hidden = 1
	if(!check_rights(R_WATCH))
		return
	var/static/t_ray_view = FALSE
	t_ray_view = !t_ray_view

	var/list/t_ray_images = list()
	var/static/list/stored_t_ray_images = list()
	for(var/obj/O in orange(client.view, src) )
		if(O.level != 1)
			continue

		if(O.invisibility == INVISIBILITY_MAXIMUM)
			var/image/I = new(loc = get_turf(O))
			var/mutable_appearance/MA = new(O)
			MA.alpha = 128
			MA.dir = O.dir
			I.appearance = MA
			t_ray_images += I
	stored_t_ray_images += t_ray_images
	if(t_ray_images.len)
		if(t_ray_view)
			client.images += t_ray_images
		else
			client.images -= stored_t_ray_images

/datum/orbit_menu
	var/mob/dead/observer/owner
	var/list/cached_orbit_data
	var/cached_orbit_data_user_ref
	var/cached_orbit_data_time = 0
	var/orbit_cache_ttl_ds = 10

/datum/orbit_menu/New(mob/dead/observer/new_owner)
	if(!istype(new_owner))
		qdel(src)
		return
	owner = new_owner
	..()

/datum/orbit_menu/Destroy()
	cached_orbit_data = null
	cached_orbit_data_user_ref = null
	owner = null
	return ..()

/datum/orbit_menu/ui_interact(mob/user, datum/tgui/ui)
	ui = SStgui.try_update_ui(user, src, ui)
	if(!ui)
		ui = new(user, src, "Orbit", "Orbit", 460, 560)
		ui.set_state(GLOB.observer_state)
		ui.set_autoupdate(FALSE)
		ui.open()

/datum/orbit_menu/ui_static_data(mob/user)
	return get_orbit_data_snapshot(user)

/datum/orbit_menu/ui_data(mob/user)
	var/list/data = list()
	data["orbiting_ref"] = owner?.orbiting_ref
	return data

/datum/orbit_menu/ui_act(action, list/params, datum/tgui/ui, datum/ui_state/state)
	if(..())
		return TRUE

	if(!istype(owner) || !isobserver(ui?.user))
		return TRUE

	switch(action)
		if("orbit")
			var/ref = params["ref"]
			if(!ref)
				return TRUE

			var/atom/movable/target = locate(ref)
			if(!istype(target))
				to_chat(ui.user, span_notice("That target is no longer available."))
				return TRUE

			if(istype(target, /mob/dead/new_player))
				to_chat(ui.user, span_notice("You cannot orbit lobby players."))
				return TRUE

			if(is_hidden_from_ghosts(target, owner))
				to_chat(ui.user, span_notice("That target is protected from ghost orbit."))
				return TRUE

			//A second click on the same target cancels instead of starting again.
			if(owner.orbiting_ref == ref)
				owner.orbiting?.end_orbit(owner)
				SStgui.update_uis(src)
				return TRUE

			owner.ManualFollow(target)
			owner.reset_perspective(null)
			if(params["auto_observe"])
				owner.do_observe(target)
			SStgui.update_uis(src)
			return TRUE

		if("refresh")
			invalidate_orbit_cache()
			ui.send_full_update()
			return TRUE

	return FALSE

/datum/orbit_menu/proc/invalidate_orbit_cache()
	cached_orbit_data = null
	cached_orbit_data_user_ref = null
	cached_orbit_data_time = 0

/datum/orbit_menu/proc/get_orbit_data_snapshot(mob/user)
	if(!istype(user))
		return build_orbit_data(user)

	var/user_ref = REF(user)
	if(cached_orbit_data && cached_orbit_data_user_ref == user_ref && (world.time - cached_orbit_data_time) <= orbit_cache_ttl_ds)
		return cached_orbit_data

	var/list/data = build_orbit_data(user)

	cached_orbit_data = data
	cached_orbit_data_user_ref = user_ref
	cached_orbit_data_time = world.time
	return data

/datum/orbit_menu/proc/build_orbit_data(mob/user)
	var/list/data = list(
		"alive" = list(),
		"dead" = list(),
		"ghosts" = list(),
	)

	var/list/namecounts_alive = list()
	var/list/namecounts_dead = list()
	var/list/namecounts_ghosts = list()
	var/list/role_color_cache = list()

	for(var/mob/M in sortmobs())
		if(M.client?.holder?.fakekey)
			continue
		if(istype(M, /mob/dead/new_player))
			continue
		if(is_hidden_from_ghosts(M, user))
			continue

		if(isobserver(M))
			append_serialized_target(data["ghosts"], M, namecounts_ghosts, role_color_cache)
			continue

		if(M.stat == DEAD)
			if(!M.mind && !M.ckey)
				continue
			append_serialized_target(data["dead"], M, namecounts_dead, role_color_cache)
			continue

		if(istype(M, /mob/living/carbon/human/species/npc/deadite))
			continue

		if(!M.mind && !M.ckey)
			continue

		append_serialized_target(data["alive"], M, namecounts_alive, role_color_cache)

	return data

/datum/orbit_menu/proc/append_serialized_target(list/bucket, atom/movable/target, list/namecounts, list/role_color_cache)
	if(!islist(bucket))
		return

	var/list/entry = serialize_atom(target, namecounts, role_color_cache)
	if(!entry)
		return

	bucket += list(entry)

/datum/orbit_menu/proc/get_role_selection_color(assigned_role, list/role_color_cache, datum/job/J = null)
	if(!assigned_role)
		return null

	if(role_color_cache)
		var/cached_color = role_color_cache[assigned_role]
		if(!isnull(cached_color))
			return cached_color || null

	var/resolved_color = null
	if(!J)
		J = SSjob.GetJob(assigned_role)
	if(J)
		var/department = SSjob.bitflag_to_department(J.department_flag, J.obsfuscated_job)
		var/list/department_colors = JCOLOR_BY_DEPARTMENT
		if(department_colors[department])
			resolved_color = department_colors[department]
		else if(J.selection_color)
			resolved_color = J.selection_color

	if(role_color_cache)
		role_color_cache[assigned_role] = resolved_color || ""

	return resolved_color

/datum/orbit_menu/proc/get_orbit_antag_group(mob/M)
	if(!istype(M) || !M.mind)
		return null

	var/special_role = M.mind.special_role
	var/assigned_role = M.mind.assigned_role || M.job

	for(var/datum/antagonist/A in M.mind.antag_datums)
		var/list/candidate = get_orbit_antag_candidate(M, A, special_role, assigned_role)
		if(candidate && candidate["group"])
			return candidate["group"]

	var/static/list/major_antag_typecache = typecacheof(list(
		/datum/antagonist/werewolf,
		/datum/antagonist/vampire,
		/datum/antagonist/lich,
	))
	var/static/list/minor_antag_typecache = typecacheof(list(
		/datum/antagonist/bandit,
		/datum/antagonist/wretch,
		/datum/antagonist/gnoll,
	))

	var/has_minor = FALSE
	for(var/datum/antagonist/A in M.mind.antag_datums)
		if(is_type_in_typecache(A, major_antag_typecache))
			return "major"
		if(is_type_in_typecache(A, minor_antag_typecache))
			has_minor = TRUE

	if(has_minor)
		return "minor"

	return null

/datum/orbit_menu/proc/get_orbit_antag_info(mob/M)
	if(!istype(M) || !M.mind)
		return null

	var/best_priority = 100000
	var/best_group = null
	var/best_label = null
	var/special_role = M.mind.special_role
	var/assigned_role = M.mind.assigned_role || M.job

	for(var/datum/antagonist/A in M.mind.antag_datums)
		var/list/candidate = get_orbit_antag_candidate(M, A, special_role, assigned_role)
		if(!candidate)
			continue

		var/candidate_priority = candidate["priority"]
		if(candidate_priority < best_priority)
			best_priority = candidate_priority
			best_group = candidate["group"]
			best_label = candidate["label"]

	if(best_group && best_label)
		return list(
			"group" = best_group,
			"label" = best_label,
		)

	return null

/datum/orbit_menu/proc/get_orbit_antag_candidate(mob/M, datum/antagonist/A, special_role, assigned_role)
	if(!istype(A))
		return null

	if(istype(A, /datum/antagonist/vampire/lord))
		return list("priority" = 10, "group" = "major", "label" = "Vampire Lord")
	if(istype(A, /datum/antagonist/vampire/ancillae))
		return list("priority" = 11, "group" = "major", "label" = "Ancillae Vampire")
	if(istype(A, /datum/antagonist/vampire/licker))
		return list("priority" = 12, "group" = "major", "label" = "Lesser Vampire")
	if(istype(A, /datum/antagonist/vampire/thinblood))
		return list("priority" = 13, "group" = "major", "label" = "Thinblood Vampire")
	if(istype(A, /datum/antagonist/vampire))
		var/datum/antagonist/vampire/V = A
		if(V.generation >= GENERATION_METHUSELAH)
			return list("priority" = 14, "group" = "major", "label" = "Vampire Lord")
		if(special_role == "Vampire Spawn")
			return list("priority" = 15, "group" = "major", "label" = "Vampire Spawn")
		return list("priority" = 16, "group" = "major", "label" = "Lesser Vampire")

	if(istype(A, /datum/antagonist/werewolf))
		if(A.name == "Lesser Verevolf")
			return list("priority" = 20, "group" = "major", "label" = "Lesser Werewolf")
		return list("priority" = 21, "group" = "major", "label" = "Werewolf")

	if(istype(A, /datum/antagonist/lich))
		return list("priority" = 30, "group" = "major", "label" = "Lich")

	if(istype(A, /datum/antagonist/skeleton/knight))
		return list("priority" = 40, "group" = "minor", "label" = "Death Knight")
	if(istype(A, /datum/antagonist/skeleton))
		if(special_role == ROLE_LICH_SKELETON)
			return list("priority" = 41, "group" = "minor", "label" = "Lich Skeleton")
		if(special_role == ROLE_NECRO_SKELETON)
			return list("priority" = 42, "group" = "minor", "label" = "Necromancer Skeleton")
		if(HAS_TRAIT(M, TRAIT_LICHLAIR))
			return list("priority" = 43, "group" = "minor", "label" = "Lich Skeleton")
		if(assigned_role == "Fortified Skeleton" || assigned_role == "Greater Skeleton")
			return list("priority" = 44, "group" = "minor", "label" = "Necromancer Skeleton")
		return list("priority" = 45, "group" = "minor", "label" = "Skeleton")

	if(istype(A, /datum/antagonist/bandit))
		return list("priority" = 50, "group" = "minor", "label" = "Bandit")
	if(istype(A, /datum/antagonist/wretch))
		return list("priority" = 51, "group" = "minor", "label" = "Wretch")
	if(istype(A, /datum/antagonist/gnoll))
		return list("priority" = 52, "group" = "minor", "label" = "Gnoll")

	var/list/extra_candidate = get_orbit_extra_antag_candidate(A, special_role)
	if(extra_candidate)
		return extra_candidate

	return null

/datum/orbit_menu/proc/get_orbit_extra_antag_candidate(datum/antagonist/A, special_role)
	var/static/list/orbit_extra_antag_definitions = list(
		list("type" = /datum/antagonist/ascendant, "group" = "major", "priority" = 60),
		list("type" = /datum/antagonist/dreamwalker, "group" = "major", "priority" = 61),
		list("type" = /datum/antagonist/unbound_death_knight, "group" = "major", "priority" = 62),
		list("type" = /datum/antagonist/zizo_knight, "group" = "major", "priority" = 63),
		list("type" = /datum/antagonist/prebel/head, "group" = "minor", "priority" = 70),
		list("type" = /datum/antagonist/prebel, "group" = "minor", "priority" = 71),
		list("type" = /datum/antagonist/aspirant, "group" = "minor", "priority" = 72),
		list("type" = /datum/antagonist/assassin, "group" = "minor", "priority" = 73),
		list("type" = /datum/antagonist/thievesguild, "group" = "minor", "priority" = 74),
	)

	for(var/list/def in orbit_extra_antag_definitions)
		if(istype(A, def["type"]))
			return list(
				"priority" = def["priority"],
				"group" = def["group"],
				"label" = A.name || special_role || "Antagonist",
			)

	return null

/datum/orbit_menu/proc/serialize_atom(atom/movable/target, list/namecounts, list/role_color_cache)
	if(!istype(target))
		return null

	var/display_name
	if(ismob(target))
		var/mob/M = target
		display_name = avoid_assoc_duplicate_keys(M.real_name || M.name, namecounts)
		if(M.real_name && M.real_name != M.name)
			display_name += " \[[M.name]\]"
	else
		display_name = avoid_assoc_duplicate_keys(target.name, namecounts)

	var/orbiter_count = 0
	if(target.orbiters)
		orbiter_count = length(target.orbiters.orbiters)

	var/list/entry = list(
		"full_name" = display_name,
		"ref" = REF(target),
		"orbiters" = orbiter_count,
	)

	if(ismob(target))
		var/mob/M = target
		if(M.stat != DEAD && !isobserver(M))
			var/list/antag_info = get_orbit_antag_info(M)
			if(antag_info)
				entry["antag_group"] = antag_info["group"]
				entry["antag_role"] = antag_info["label"]
			else
				var/antag_group = get_orbit_antag_group(M)
				if(antag_group)
					entry["antag_group"] = antag_group
		if(M.mind?.assigned_role)
			var/assigned_role = M.mind.assigned_role
			entry["role"] = assigned_role
			var/datum/job/J = SSjob.GetJob(assigned_role)
			if(J)
				var/job_department = SSjob.bitflag_to_department(J.department_flag, J.obsfuscated_job)
				if(job_department)
					entry["department"] = job_department
			var/selection_color = get_role_selection_color(assigned_role, role_color_cache, J)
			if(selection_color)
				entry["selection_color"] = selection_color
		if(M.job)
			entry["job"] = M.job
		if(M.advjob) //Subclass, such as the one a Wretch or an Adventurer picks.
			entry["subclass"] = M.advjob
		if(isliving(M))
			var/mob/living/L = M
			if(L.maxHealth > 0)
				entry["health_percent"] = round(clamp((L.health / L.maxHealth) * 100, 0, 100))
		if(istype(M, /mob/living/carbon/human/species/npc/deadite))
			entry["role"] = "Deadite NPC"

	return entry
